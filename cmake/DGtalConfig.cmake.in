# - Config file for the DGtal package
# It defines the following variables
#  DGTAL_INCLUDE_DIRS - include directories for DGtal
#  DGTAL_LIBRARY_DIRS - library directories for DGtal (normally not used!)
#  DGTAL_LIBRARIES    - libraries to link against
#  DGTAL_VERSION      - version of the DGtal library
@PACKAGE_INIT@
include(CMakeFindDependencyMacro)
list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR})

set(DGTAL_VERSION "@DGTAL_VERSION@")

#### Required dependencies  ####
find_dependency(Boost REQUIRED
  @Boost_HINTS@
  )
find_dependency(ZLIB REQUIRED
  @ZLIB_HINTS@
  )

set(WITH_EIGEN 1)
include(eigen)
set(DGtalLibDependencies ${DGtalLibDependencies} Eigen3::Eigen)

if (@LIBIGL_FOUND_DGTAL@) 
  include(libigl)
  set(DGTAL_WITH_LIBIGL 1)
  set(DGtalLibDependencies ${DGtalLibDependencies} igl::core)
endif()

#### Optionnal dependencies  ####

if(@GMP_FOUND_DGTAL@) #if GMP_FOUND_DGTAL
  find_package(GMP REQUIRED
    @GMP_HINTS@
    )
  set(DGTAL_WITH_GMP 1)
endif()


if(@ITK_FOUND_DGTAL@) #if ITK_FOUND_DGTAL
  set (DGTAL_WITH_ITK 1)
  find_dependency(ITK REQUIRED
    @ITK_HINTS@
    )
    include(${ITK_USE_FILE})
endif()

if(@CAIRO_FOUND_DGTAL@) #if CAIRO_FOUND_DGTAL
  find_package(Cairo REQUIRED
    @Cairo_HINTS@
    )
  set(DGTAL_WITH_CAIRO 1)
endif()

if(@HDF5_FOUND_DGTAL@) #if HDF5_FOUND_DGTAL
  find_dependency(HDF5 REQUIRED HL C
    @HDF5_HINTS@
    )
  set (DGTAL_WITH_HDF5 1)
endif()

if (@QGLVIEWER_FOUND_DGTAL@) #if QGLVIEWER_FOUND_DGTAL
  find_package(QGLVIEWER REQUIRED
    @QGLVIEWER_HINTS@
    )
  set(WITH_VISU3D_QGLVIEWER 1)
endif (@QGLVIEWER_FOUND_DGTAL@)

#-- We force the Qt rediscovering
if (@QT5_FOUND_DGTAL@) #if QT5_FOUND_DGTAL
  find_dependency(Qt5 COMPONENTS Widgets OpenGL Xml REQUIRED
    @Qt5_HINTS@
    )
  set(WITH_QT5 1)
endif (@QT5_FOUND_DGTAL@)

if(@OPENMP_FOUND_DGTAL@) #if OPENMP_FOUND_DGTAL
   include(openmp)
   set(DGtalLibDependencies ${DGtalLibDependencies} OpenMP::OpenMP_CXX)
  set(DGTAL_WITH_OPENMP 1)
endif()

if(@CGAL_FOUND_DGTAL@) #if CGAL_FOUND_DGTAL
  find_dependency(CGAL COMPONENTS Core
    @CGAL_HINTS@
    )
  set (DGTAL_WITH_CGAL 1)
endif()

if(@FFTW3_FOUND_DGTAL@) #if FFTW3_FOUND_DGTAL
  find_package(FFTW3 REQUIRED
    @FFTW3_HINTS@
    )
  set(DGTAL_WITH_FFTW3 1)
endif()

include("${CMAKE_CURRENT_LIST_DIR}/DGtalTargets.cmake")