# - Config file for the DGtal package
# It defines the following variables
#  DGTAL_INCLUDE_DIRS - include directories for DGtal
#  DGTAL_LIBRARY_DIRS - library directories for DGtal (normally not used!)
#  DGTAL_LIBRARIES    - libraries to link against
#  DGTAL_VERSION      - version of the DGtal library
@PACKAGE_INIT@

set(DGTAL_VERSION "@DGTAL_VERSION@")

include(CMakeFindDependencyMacro)

#### Required dependencies  ####
find_dependency(Boost REQUIRED
  HINTS "@BOOST_DIR@"
  )
find_dependency(ZLIB REQUIRED
  HINTS "@ZLIB_DIR@"
  )

if(@GMP_FOUND_DGTAL@) #if GMP_FOUND_DGTAL
  find_dependency(GMP REQUIRED
    HINTS "@GMP_DIR@"
    )
  set(WITH_GMP 1)
endif()

if(@MAGICK++_FOUND_DGTAL@) #if MAGICK++_FOUND_DGTAL
  find_dependency(Magick REQUIRED
    HINTS "@Magick_DIR@"
    )
  set(WITH_MAGICK 1)
endif()

if(@ITK_FOUND_DGTAL@) #if ITK_FOUND_DGTAL
  set(WITH_ITK 1)
  find_dependency(ITK REQUIRED
    HINTS "@ITK_DIR@"
    )
endif()

if(@CAIRO_FOUND_DGTAL@) #if CAIRO_FOUND_DGTAL
  find_dependency(Cairo REQUIRED
    HINTS "@Cairo_DIR@"
    )
  set(WITH_CAIRO 1)
endif()

if(@HDF5_FOUND_DGTAL@) #if HDF5_FOUND_DGTAL
  find_dependency(HDF5 REQUIRED HL C
    HINTS "@HDF5_DIR@"
    )
  set(WITH_HDF5 1)
endif()

if (@QGLVIEWER_FOUND_DGTAL@) #if QGLVIEWER_FOUND_DGTAL
  find_dependency(QGLVIEWER REQUIRED
    HINTS "@QFLVIEWER_DIR@"
    )
  set(WITH_VISU3D_QGLVIEWER 1)
endif (@QGLVIEWER_FOUND_DGTAL@)

#-- We force the Qt rediscovering
if (@QT4_FOUND_DGTAL@) #if QT4_FOUND_DGTAL
  find_package(Qt4 REQUIRED COMPONENTS QtCore QtGUI QtXml QtOpenGL)
  set(QT_USE_QTXML 1)
  set(WITH_QT4 1)
  # include(${QT_USE_FILE})
  # set(DGTAL_INCLUDE_DIRS ${DGTAL_INCLUDE_DIRS} "${QT_INCLUDE_DIR}")
endif (@QT4_FOUND_DGTAL@)

if (@QT5_FOUND_DGTAL@) #if QT5_FOUND_DGTAL
  find_dependency(Qt5 COMPONENTS Widgets OpenGL Xml REQUIRED
    HINTS "@Qt5_DIR@"
    )
  set(WITH_QT5 1)
  # set(DGTAL_Qt5_DIR @Qt5_DIR@)
  # set(DGTAL_INCLUDE_DIRS ${DGTAL_INCLUDE_DIRS}
  #                        "${Qt5Widgets_INCLUDE_DIRS}"
  #                        "${Qt5OpenGL_INCLUDE_DIRS}"
  #                        "${Qt5Xml_INCLUDE_DIRS}")
endif (@QT5_FOUND_DGTAL@)

if(@OPENMP_FOUND_DGTAL@) #if OPENMP_FOUND_DGTAL
  find_dependency(OpenMP REQUIRED
    HINTS "@OpenMP_DIR@"
    )
  set(WITH_OPENMP 1)
endif()


if(@EIGEN_FOUND_DGTAL@) #if EIGEN_FOUND_DGTAL
  find_dependency(Eigen3 REQUIRED
    HINTS "@Eigen3_DIR@"
    )
  set(WITH_EIGEN 1)
  set(DGTAL_INCLUDE_DIRS ${DGTAL_INCLUDE_DIRS} "@EIGEN3_INCLUDE_DIR@" )
endif()


if(@CGAL_FOUND_DGTAL@) #if CGAL_FOUND_DGTAL
  find_dependency(CGAL COMPONENTS Core Eigen3
    HINTS "@CGAL_DIR@"
    )
  set(WITH_CGAL 1)
endif()

if(@FFTW3_FOUND_DGTAL@) #if FFTW3_FOUND_DGTAL
  find_dependency(FFTW3 REQUIRED
    HINTS "@FFTW3_DIR@"
    )
  set(WITH_FFTW3 1)
endif()

get_filename_component(DGTAL_CMAKE_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH)
if(NOT TARGET DGtal)
  # Our imported targets, including dependencies
  include ("${DGTAL_CMAKE_DIR}/DGtalLibraryDepends.cmake")
endif()

set(DGTAL_LIBRARIES DGtal)
get_target_property(DGTAL_INCLUDE_DIRS DGtal INTERFACE_INCLUDE_DIRECTORIES)
